/	Update all stores to accept new itemtypes
COPY_EXISTING_REGEXP	~.*\.sto~	override
	READ_SHORT	0x8	type
	READ_LONG	0x2c	buy_off
	READ_LONG	0x30	buy_num
	SET	buy_add = 0	//	Number of categories to add, to update offsets
	SET	add_category = 0	//	Initialize check variable
	FOR	(i = 0; i < 3; ++i)	BEGIN	SET $add_category(~%i%~) = 1	END	//	Initialize check variable
	FOR	(i = 0; i < buy_num; ++i)	BEGIN
		READ_BYTE	(buy_off + i * 0x4)	category
		PATCH_MATCH	category WITH
			15 16 17 18 19 20 21 22 23 24 25 26 27 29 30 WHEN add_category = 0	BEGIN	// Match any Weapon Categories
				++add_category
			END
			69	BEGIN	//	Match Bastard Sword Category / No Unnecessary Duplication
				SET add_category_0 = 0
			END
			katana	BEGIN	//	Match Category added for Katana / No Unnecessary Duplication
				SET add_category_1 = 0
			END
			hvy_hamm	BEGIN	//	Match Category added for Heavy Hammers / No Unnecessary Duplication
				SET add_category_2 = 0
			END
			DEFAULT
		END
	END
	//	Add categories
	PATCH_IF	add_category	BEGIN
		PATCH_IF	add_category_0	BEGIN
			INSERT_BYTES	(buy_off + buy_num * 0x4)	0x4
			WRITE_BYTE	(buy_off + buy_num * 0x4)	69
			++buy_add
		END
		PATCH_IF	add_category_1	BEGIN
			INSERT_BYTES	(buy_off + buy_num * 0x4)	0x4
			WRITE_BYTE	(buy_off + buy_num * 0x4)	katana
			++buy_add
		END
		PATCH_IF	add_category_2	BEGIN	
			INSERT_BYTES	(buy_off + buy_num * 0x4)	0x4
			WRITE_BYTE	(buy_off + buy_num * 0x4)	hvy_hamm
			++buy_add
		END
		//	Update offsets
		WRITE_LONG	0x30	(THIS + buy_add)
		READ_LONG	0x34	items_off
		PATCH_IF	items_off >= buy_off	BEGIN	
			WRITE_LONG	0x34	(THIS + buy_add)
		END
		READ_LONG	0x4c	drink_off
		PATCH_IF	drink_off >= buy_off	BEGIN
			WRITE_LONG	0x4c	(THIS + buy_add)
		END
		READ_LONG	0x70	cures_off
		PATCH_IF	cures_off >= buy_off	BEGIN
			WRITE_LONG	0x70	(THIS + buy_add)
		END
	END
BUT_ONLY